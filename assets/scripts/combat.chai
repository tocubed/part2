global arena_loc = Location(3000, 3000);

global saved_locations;
global saved_facing;

global combat_enemy;
global combat_enemy_loc;

global combat_locations;

global enemy_action;

global enemy_health;
global follower_healths;

def teleport_to_arena()
{
	saved_locations = EntityLocationMap();
	saved_facing = EntityDirectionMap();

	combat_locations = EntityLocationMap();

	saved_locations[player] = location(player);
	saved_facing[player] = facing(player);

	var arena_ctr = Location(arena_loc.x + 15, arena_loc.y + 15);

	combat_enemy_loc = Location(arena_ctr.x, arena_ctr.y - 4);

	saved_locations[combat_enemy] = location(combat_enemy);
	saved_facing[combat_enemy] = facing(combat_enemy);

	combat_locations[combat_enemy] = combat_enemy_loc;
	script.teleport(combat_enemy, combat_enemy_loc);
	script.face(combat_enemy, DOWN);

	combat_locations[player] = Location(arena_ctr.x, arena_ctr.y);
	script.teleport(player, Location(arena_ctr.x, arena_ctr.y));
	script.face(player, UP);


	var followers = range(followers(player));
	var follower_count = 0;

	for(follower: followers)
	{
		follower_count += 1;

		saved_locations[follower] = location(follower);
		saved_facing[follower] = facing(follower);

		var follower_loc = Location(arena_ctr.x - 4, arena_ctr.y + 2);
		follower_loc.x += ((2 * follower_count - 1) % 9);
		follower_loc.y += ((2 * follower_count - 1) / 9);

		combat_locations[follower] = follower_loc;
		script.teleport(follower, follower_loc);
		script.face(follower, UP);
		tag_follower(follower, false);
	}
}

global follower_choices;
global follower_actions;

global index_to_follower;

def follower_to_index(Entity f)
{
	var follower = Entity(f);

	for(var i = 0; i < index_to_follower.size(); ++i)
	{
		if(index_to_follower[i] == f)
		{
			return i;
		}
	}
}

def all_followers_dead()
{
	var all_followers_dead = true;

	for(health: range(follower_healths))
	{
		all_followers_dead = (health <= 0) && all_followers_dead;
	}

	return all_followers_dead;
}

def enemy_dead()
{
	return enemy_health <= 0;
}

def combat_end()
{
	return enemy_dead() || all_followers_dead();
}

def kill_follower(int i)
{
	follower_choices.erase_at(i);
	follower_actions.erase_at(i);

	follower_healths.erase_at(i);

	var i_followers = followers(index_to_follower[i]);
	for(var j = i_followers.size(); j > 1; --j)
	{
		saved_locations[i_followers[j - 1]] = saved_locations[i_followers[j - 2]];
		saved_facing[i_followers[j - 1]] = saved_facing[i_followers[j - 2]];
	}

	if(i_followers.size() > 0)
	{
		saved_locations[i_followers[0]] = saved_locations[index_to_follower[i]];
		saved_facing[i_followers[0]] = saved_facing[index_to_follower[i]];
	}

	script.kill_character(index_to_follower[i]);
	index_to_follower.erase_at(i);
}

def check_if_followers_died()
{
	var to_be_killed = [];

	for(var i = 0; i < follower_healths.size(); ++i)
	{
		if(follower_healths[i] <= 0)
		{
			to_be_killed.push_back(i);
		}
	}

	for(follower_i: to_be_killed)
	{
		kill_follower(follower_i);
	}
}

def combat_loop()
{
	check_if_followers_died();

	prompt("Which follower will you call on?", follower_choices, follower_actions);
}

def move_back_to_original_positions(callback)
{
	script.walk_to(me, combat_locations[me], fun[callback]()
	{
		if(me == combat_enemy)
		{
			script.face(me, DOWN);
		}
		else
		{
			script.face(me, UP);
		}
		script.walk_to(attacking, combat_locations[attacking], fun[callback]()
		{
			if(attacking == combat_enemy)
			{
				script.face(attacking, DOWN);
			}
			else
			{
				script.face(attacking, UP);
			}
			callback();
		});
	});
}

def check_defeated(callback)
{
	var defeat_me = fun(callback)
	{
		var defeated = false;

		if(me == combat_enemy)
		{
			if(enemy_health <= 0)
			{
				defeated = true;
			}
		}
		else
		{
			if(follower_healths[follower_to_index(me)] <= 0)
			{
				defeated = true;
			}
		}
		
		if(defeated)
		{
			return fun[callback]()
			{
				resume_combat = callback;
				eval(get_script(me, "OnDefeat"));
			};
		}
		else
		{
			return callback;
		}
	};

	var defeat_attacking = fun(callback)
	{
		var defeated = false;

		if(attacking == combat_enemy)
		{
			if(enemy_health <= 0)
			{
				defeated = true;
			}
		}
		else
		{
			if(follower_healths[follower_to_index(attacking)] <= 0)
			{
				defeated = true;
			}
		}
		
		if(defeated)
		{
			return fun[callback]()
			{
				resume_combat = fun[callback]()
				{
					var temp = me;
					me = attacking;
					attacking = me;
					callback();
				};
				
				var temp = me;
				me = attacking;
				attacking = me;

				eval(get_script(attacking, "OnDefeat"));
			};
		}
		else
		{
			return callback;
		}
	};

	defeat_me(fun[callback, defeat_attacking]()
	{
		defeat_attacking(fun[callback]()
		{
			callback();
		})();
	})();
}

global resume_combat;

def make_follower_action(Entity f)
{
	var follower = Entity(f);

	var action = fun[follower]()
	{
		me = follower;
		attacking = combat_enemy;

		resume_combat = fun[follower]() {
			me = combat_enemy;
			attacking = follower;

			resume_combat = fun()
			{
				move_back_to_original_positions(fun()
				{
					check_defeated(fun()
					{
						if(combat_end())
						{
							end_combat();
						}
						else
						{
							combat_loop();
						}
					});
				});
			};

			enemy_action();
		};

		eval(get_script(follower, "OnCombat"));
	};

	return action;
}

def init_enemy()
{
	enemy_health = 100;

	enemy_action = fun()
	{
		eval(get_script(combat_enemy, "OnCombat"));
	};
}

def init_followers()
{
	follower_choices = [];
	follower_actions = [];

	follower_healths = [];

	index_to_follower = [];

	var followers = range(followers(player));
	for(follower: followers)
	{
		follower_choices.push_back(name(follower));
		follower_actions.push_back(make_follower_action(follower));
		
		follower_healths.push_back(100);

		index_to_follower.push_back(follower);
	}
}

global end_combat_callback;

def end_combat()
{
	show_healthbars(false);

	if(all_followers_dead())
	{
		fade(true, fun()
		{
			dialog("<i>Without any followers to guide him, the Kid becomes lost in a jungle and invents coconut milk.</i>", fun()
			{
				dialog("<b>GAME OVER</b>");
			});
		});
	}
	else
	{
		fade(true, fun()
		{
			freeze_player(false);

			fade(false);

			script.teleport(combat_enemy, saved_locations[combat_enemy]);
			script.face(combat_enemy, saved_facing[combat_enemy]);

			script.teleport(player, saved_locations[player]);
			script.face(player, saved_facing[player]);

			var followers = range(followers(player));
			for(follower: followers)
			{
				script.teleport(follower, saved_locations[follower]);
				script.face(follower, saved_facing[follower]);

				tag_follower(follower, true);
			}

			end_combat_callback();

			script.unload_map(arena_loc);
		});
	}
}


def do_combat(string arena, Entity enemy, Function callback)
{
	script.load_map("assets/maps/arenas/"+arena+".tmx", arena_loc);

	combat_enemy = enemy;
	end_combat_callback = callback;

	freeze_player(true);

	fade(true, fun()
	{
		fade(false);

		teleport_to_arena();
		init_enemy();
		init_followers();

		show_healthbars(true);

		combat_loop();
	});
}

// Stuff that can be used by OnCombat
global me;
global attacking;

def damage(Entity e, int damage)
{
	if(e == combat_enemy)
	{
		enemy_health -= damage;
	}
	else
	{
		var index = follower_to_index(e);

		follower_healths[index] -= damage;
	}
}

def walk_in_enemy_melee_range(Function callback)
{
	var attacking_loc = location(attacking);

	if(me == combat_enemy)
	{
		attacking_loc.y -= 1;
	}
	else
	{
		attacking_loc.y += 1;
	}

	script.walk_to(me, attacking_loc, fun[callback]()
	{
		if(me == combat_enemy)
		{
			script.face(me, DOWN);
		}
		else
		{
			script.face(me, UP);
		}
		callback();
	});
}
